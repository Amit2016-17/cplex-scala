/*
 *  Source file provided under Apache License, Version 2.0, January 2004,
 *  http://www.apache.org/licenses/
 *  (c) Copyright DecisionBrain SAS 2016,2020
 */

group 'com.decisionbrain'
version '1.7.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply from: 'gradle/cplex_plugin.gradle'

//
// DecisionBrain Nexus Repository for Jar files and Docker images
//

ext {
    if (!project.hasProperty('NEXUS_URL'))
        NEXUS_URL = "None"
    if (!project.hasProperty('MAVEN_USER'))
        MAVEN_USER= "None"
    if (!project.hasProperty('MAVEN_PASSWORD'))
        MAVEN_PASSWORD = "None"
}

println("Nexus URL: $NEXUS_URL")
println("Maven user: $MAVEN_USER")

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task scaladocJar(type: Jar) {
    archiveClassifier = 'scaladoc'
    from scaladoc.destinationDir
}

apply plugin: 'maven-publish'

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact scaladocJar
        }
    }
    repositories {
        maven {
            name = "Nexus"
            url = "$NEXUS_URL/repository/" + (version.endsWith('SNAPSHOT') ? "snapshots" : "releases")
            credentials {
                username = "$MAVEN_USER"
                password = "$MAVEN_PASSWORD"
            }
        }
    }
}

//
// CPLEX Home
//

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileScala {
    targetCompatibility = 1.8
}

project.ext.set("copyright", new File("copyright.txt").text)

repositories {
    // HACK: if we ask Gradle to refresh the dependencies, don't look at local repo!
    if (!gradle.startParameter.refreshDependencies) {
        mavenLocal()
    }
    mavenCentral()
}

configurations.all {
    // check for SNAPSHOT Updates every time instead of every 24h
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// Custom sourceSets
sourceSets {
    test {
        scala {
            srcDirs += 'src/examples/scala'
        }
        java {
            srcDirs += 'src/examples/java'
        }
    }
}

eclipse {
    classpath {
        downloadSources=true
    }
}

idea {
    module {
        downloadSources = true
    }
}


javadoc {
    doLast {
        copy {
            from "src"
            include "**/doc-files/*.png"
            into "$buildDir/docs/javadoc"
        }
    }
    options.setHeader("<script type=\"text/javascript\" src=\"http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML\"></script>")
    options.showFromPrivate()
    options.setBottom("$copyright")
}

scaladoc {
    doLast {
        copy {
            from "src"
            include "**/doc-files/*.png"
            into "$buildDir/docs/scaladoc"
        }
    }
}

ext {
    scalaVersion = '2.13.1'
    scalaVersionShort = '2.13'
    scalaTestVersion = '3.0.8'
}

dependencies {
    compile fileTree(dir: cplexStudioHome + "/cpoptimizer/lib", include: ['*.jar'])
    compile fileTree(dir: cplexStudioHome + "/cplex/lib", include: ['*.jar'])

    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'commons-lang:commons-lang:2.6'
    compile("org.scala-lang:scala-library:$scalaVersion")

    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile('org.mockito:mockito-all:1.9.5')
    testCompile("junit:junit:4.12")
    testCompile("org.scalatest:scalatest_$scalaVersionShort:$scalaTestVersion")
    testCompile 'org.slf4j:slf4j-simple:1.7.10'
}


task listJars {
    doLast {
        configurations.compile.each { File file -> println file.name }
    }
}

wrapper {
    gradleVersion = '5.6.2'
    description = 'Install Gradle Wrapper'
}

